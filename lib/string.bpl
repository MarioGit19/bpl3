# String standard library

export [String];

extern strlen(s: string) ret int;
extern strcpy(dst: string, src: string) ret string;
extern strcmp(s1: string, s2: string) ret int;
extern strcat(dst: string, src: string) ret string;
extern malloc(size: long) ret string;
extern free(ptr: string) ret void;

struct String {
    data: string,
    length: int,
    frame new(text: string) ret String {
        local s: String;
        if (text == null) {
            s.data = null;
            s.length = 0;
            return s;
        }
        local len: int = strlen(text);
        s.length = len;
        s.data = malloc(cast<long>(len + 1));
        strcpy(s.data, text);
        return s;
    }

    frame destroy(this: *String) {
        if (this.data != null) {
            free(this.data);
            this.data = null;
        }
        this.length = 0;
    }

    frame cstr(this: *String) ret string {
        return this.data;
    }

    frame assign(this: *String, text: string) {
        this.destroy();
        local newStr: String = String.new(text);
        this.data = newStr.data;
        this.length = newStr.length;
    }

    # Returns true when the string has no characters.
    frame isEmpty(this: String) ret bool {
        return this.length == 0;
    }

    # Create a deep copy of this string.
    frame clone(this: String) ret String {
        if (this.data == null) {
            return String.new(null);
        }
        return String.new(this.data);
    }

    frame includes(this: *String, substr: string) ret bool {
        if ((this.data == null) || (substr == null)) {
            return false;
        }
        local substrLen: int = strlen(substr);
        if ((substrLen == 0) || (substrLen > this.length)) {
            return false;
        }
        local i: int = 0;
        loop (i <= (this.length - substrLen)) {
            local found: bool = true;
            local j: int = 0;
            loop (j < substrLen) {
                if (*(this.data + cast<long>(i + j)) != *(substr + cast<long>(j))) {
                    found = false;
                    break;
                }
                j = j + 1;
            }
            if (found) {
                return true;
            }
            i = i + 1;
        }
        return false;
    }

    # Operator overloading: String concatenation with +
    frame __add__(this: *String, other: String) ret String {
        if (this.data == null) {
            return other.clone();
        }
        if (other.data == null) {
            return this.clone();
        }
        local newLen: int = this.length + other.length;
        local newData: string = malloc(cast<long>(newLen + 1));
        strcpy(newData, this.data);
        strcat(newData, other.data);
        local result: String;
        result.data = newData;
        result.length = newLen;
        return result;
    }

    # Operator overloading: String concatenation with + (string literal overload)
    # Allows: str + "literal" without needing String.new()
    frame __add__(this: *String, other: string) ret String {
        local otherStr: String = String.new(other);
        local result: String = this.__add__(otherStr);
        otherStr.destroy();
        return result;
    }

    # Operator overloading: String equality with ==
    frame __eq__(this: *String, other: String) ret bool {
        if ((this.data == null) && (other.data == null)) {
            return true;
        }
        if ((this.data == null) || (other.data == null)) {
            return false;
        }
        if (this.length != other.length) {
            return false;
        }
        return strcmp(this.data, other.data) == 0;
    }

    # Operator overloading: String inequality with !=
    frame __ne__(this: *String, other: String) ret bool {
        return !this.__eq__(other);
    }

    # Operator overloading: String less than with <
    frame __lt__(this: *String, other: String) ret bool {
        if ((this.data == null) || (other.data == null)) {
            return false;
        }
        return strcmp(this.data, other.data) < 0;
    }

    # Operator overloading: String less than or equal with <=
    frame __le__(this: *String, other: String) ret bool {
        if ((this.data == null) || (other.data == null)) {
            return false;
        }
        return strcmp(this.data, other.data) <= 0;
    }

    # Operator overloading: String greater than with >
    frame __gt__(this: *String, other: String) ret bool {
        if ((this.data == null) || (other.data == null)) {
            return false;
        }
        return strcmp(this.data, other.data) > 0;
    }

    # Operator overloading: String greater than or equal with >=
    frame __ge__(this: *String, other: String) ret bool {
        if ((this.data == null) || (other.data == null)) {
            return false;
        }
        return strcmp(this.data, other.data) >= 0;
    }

    # Operator overloading: In-place concatenation with <<
    # Usage: str << "text" modifies str in place and returns it
    frame __lshift__(this: *String, other: String) ret String {
        if (other.data == null) {
            return *this;
        }
        if (this.data == null) {
            local newStr: String = other.clone();
            this.data = newStr.data;
            this.length = newStr.length;
            return *this;
        }
        local newLen: int = this.length + other.length;
        local newData: string = malloc(cast<long>(newLen + 1));
        strcpy(newData, this.data);
        strcat(newData, other.data);
        free(this.data);
        this.data = newData;
        this.length = newLen;
        return *this;
    }

    # Operator overloading: In-place concatenation with << (string literal overload)
    # Allows: str << "literal" without needing String.new()
    frame __lshift__(this: *String, other: string) ret String {
        local otherStr: String = String.new(other);
        local result: String = this.__lshift__(otherStr);
        otherStr.destroy();
        return result;
    }

    frame fromInt(val: long) ret String {
        local buf: string = malloc(32); # Enough for 64-bit int
        sprintf(buf, "%ld", val);
        local s: String;
        s.data = buf;
        s.length = strlen(buf);
        return s;
    }

    frame fromAddress(addr: long) ret String {
        local buf: string = malloc(32); # Enough for 64-bit hex
        sprintf(buf, "%#lx", addr);
        local s: String;
        s.data = buf;
        s.length = strlen(buf);
        return s;
    }
}

extern sprintf(str: string, format: string, ...) ret int;
